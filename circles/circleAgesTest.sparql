PREFIX :      <http://www.swows.org/examples/circles#>
PREFIX tuio:  <http://www.swows.org/tuio#>
PREFIX vp:    <http://www.swows.org/viewport#> 
PREFIX swi:   <http://www.swows.org/instance#>
PREFIX dir:   </home/miguel/TUIO/circles/>

CONSTRUCT {
  ?circle :source ?tracked .
  ?circle :creationTime ?newCircleCreationTime .
  ?circle :centerX ?circleCenterX .
  ?circle :centerY ?circleCenterY .
}
WHERE {
 {
  GRAPH dir:viewPortInfo.n3 {
    swi:GraphRoot
      vp:vpWidth ?vpWidth ;
      vp:vpHeight ?vpHeight .
  } .
  {
      SELECT
        ?tracked
        ( MAX(?circleCreationTime) + ?circlePeriod  AS ?newCircleCreationTime )
      WHERE {
        GRAPH dir:circleAges.n3 {
          ?circle :source ?tracked .
          ?circle :creationTime ?circleCreationTime .
        } .
        GRAPH dir:config.n3 {
          swi:GraphRoot
            :circlePeriod ?circlePeriod
        } .
      } GROUP BY ?tracked ?circlePeriod
  } 
  UNION {
      SELECT
        ?tracked
        ( ?creationTime  AS ?newCircleCreationTime )
      WHERE {
        ?tracked a tuio:Cursor .
        ?tracked tuio:source tuio:defaultSource .
        ?tracked tuio:creationTime ?creationTime .
        FILTER NOT EXISTS { 
          GRAPH dir:circleAges.n3 {
            ?circle :source ?tracked .
          }
        }
      }
  } .
  ?tracked tuio:position ?point .
  ?point tuio:x ?trackedX .
  ?point tuio:y ?trackedY .
  BIND( ?trackedX * ?vpWidth AS ?circleCenterX ) .
  BIND( ?trackedY * ?vpHeight AS ?circleCenterY ) .
  tuio:defaultSource tuio:updateTime ?updateTime .
  FILTER( ?newCircleCreationTime <= ?updateTime ) .
  BIND( URI(CONCAT(STR(:circle_), MD5(STR(?tracked)+STR(?newCircleCreationTime)))) AS ?circle ) .
 } 
 UNION {
  GRAPH dir:viewPortInfo.n3 {
    swi:GraphRoot
      vp:vpWidth ?vpWidth ;
      vp:vpHeight ?vpHeight .
  } .
  GRAPH dir:circles.n3 {
    ?circle :radius ?radius
  }
  GRAPH dir:circleAges.n3 {
    ?circle :source ?tracked .
    ?circle :creationTime ?newCircleCreationTime .
    ?circle :centerX ?circleCenterX .
    ?circle :centerY ?circleCenterY .
  } .
  FILTER( ?radius * ?radius <= ?vpWidth *?vpWidth + ?vpHeight * ?vpHeight ) .
 } .
}

